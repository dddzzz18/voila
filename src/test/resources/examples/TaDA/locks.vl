struct memcell {
  int f;
}

region Lock(id a, int lvl, memcell x)
  guards { unique G; }
  interpretation {
    x.f |-> ?v && (v == 0 || v == 1)
  }
  state { v }
  actions {
    G: 0 ~> 1;
    G: 1 ~> 0;
  }

//predicate U(id a, int lvl, memcell x) {
//  Lock(a, lvl, x, 0) && G@a
//}
//
//predicate L(id a, int lvl, memcell x) {
//  Lock(a, lvl, x, 1) && G@a
//}

predicate Inv() /* Invariant of CAP lock (left abstract) */

region CAPLock(id r, int alvl, id a, int lvl, memcell x)
  guards {
    duplicable Z;
    unique K;
  }
  interpretation {
       Lock(a, lvl, x, ?v) && lvl < alvl && G@a // Included in both L(a, lvl, x) and U(a, lvl, x)
    && (v == 0 ==> K@r && Inv())
    && (v == 1 ==> true)
  }
  state { v }
  actions {
    Z: 0 ~> 1;
    K: 1 ~> 0;
  }

predicate isLock(id r, int alvl, id a, int lvl, memcell x) {
  CAPLock(r, alvl, a, lvl, x, ?v) && (v in Set(0, 1))
}

predicate Locked(id r, int alvl, id a, int lvl, memcell x) {
  CAPLock(r, alvl, a, lvl, x, ?v) && v == 1 && K@r
}



primitive_atomic procedure CAS(memcell x, int now, int thn) returns (bool ret)
  requires x.f |-> ?v;
  ensures  v == now
              ? ret && x.f |-> thn
              : !ret && x.f |-> v;



abstract_atomic procedure unlock(id a, int lvl, memcell x)
  interference ?s in Set(1) on a;
  requires Lock(a, lvl, x, s) && G@a; // L(a, lvl, x)
  ensures Lock(a, lvl, x, 0) && G@a; // U(a, lvl, x)
{
  // unfold L(a, lvl, x);

  make_atomic
    using Lock(a, lvl, x) with G@a;
  {
    update_region
      using Lock(a, lvl, x);
    {
      x.f := 0;
    }
  }

  // fold U(a, lvl, x);
}


procedure non_atomic_unlock(id r, int alvl, id a, int lvl, memcell x)
  requires Locked(r, alvl, a, lvl, x);
  requires Inv();
{
  unfold Locked(r, alvl, a, lvl, x);
  // unfold CAPLock(r, alvl, a, lvl, x);
  // fold L(a, lvl, x);

  use_atomic
    using CAPLock(r, alvl, a, lvl, x) with K@r;
  {
    unlock(a, lvl, x);
  }
}




abstract_atomic procedure lock(id a, int lvl, memcell x)
  interference ?s in Set(0, 1) on a;
  // requires s == 0 ? U(a, lvl, x) : L(a, lvl, x);
  requires Lock(a, lvl, x, s) && G@a;
  // ensures L(a, lvl, x) && s == 0;
  ensures Lock(a, lvl, x, 1) && G@a && s == 0;
{
  bool b;

  // if (s == 0) { unfold U(a, lvl, x); }
  // else { unfold L(a, lvl, x); }

  make_atomic
    using Lock(a, lvl, x) with G@a;
  {
    do
      invariant Lock(a, lvl, x);
      invariant !b ==> a |=> <D>;
      invariant  b ==> a |=> (0, 1);
    {
      update_region
        using Lock(a, lvl, x);
      {
        b := CAS(x, 0, 1);
      }
    } while (!b);
  }

  // fold L(a, lvl, x);
}

procedure non_atomic_lock(id r, int alvl, id a, int lvl, memcell x)
  requires isLock(r, alvl, a, lvl, x) && Z@r;
  ensures  Locked(r, alvl, a, lvl, x);
  // ensures  isLock(r, a, x);
{
  unfold isLock(r, alvl, a, lvl, x);

  use_atomic
    using CAPLock(r, alvl, a, lvl, x) with Z@r;
  {
    lock(a, lvl, x);
  }

  fold Locked(r, alvl, a, lvl, x);
}
