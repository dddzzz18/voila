region Cell(id s, memcell x)
  guards { unique G; }
  interpretation { x.f |-> ?v }
  state { v }
  actions {
    G: ?m ~> Int;
  }

abstract_atomic procedure spinCAS(id s,
                                  memcell x,
                                  set<int> from)
                                  // lambda<bool(int)> cas_if,
                                  // lambda<int(int)> cas_to)
  interference ?m in from on s;
  requires Cell(s, x, m) && G@s;
  // requires forall v: Int :: v != cas_to(v) // Voila issue #8
  ensures  Cell(s, x, ?n) && G@s;
  // ensures  cas_if(m);
  // ensures  n == cas_to(m);
{
  int v;
  bool b;

  bool cas_if;
  int cas_to;

  make_atomic
    using Cell(s, x) with G@s;
  {
    do
      invariant Cell(s, x);
      invariant !b ==> s |=> <D>;
//      invariant  b ==> s |=> (v, cas_to(v)) && (v in from) && cas_if(v);
      invariant  b ==> s |=> (v, cas_to) && (v in from) && cas_if;
    {
      open_region
        using Cell(s, x);
      {
        v := x.f;
      }

      havoc cas_if;
      havoc cas_to;
      assume cas_to != v; // Voila issue #8

//      if (cas_if(v)) {
      if (cas_if) {
        update_region
          using Cell(s, x);
        {
//          b := CAS(x, v, cas_to(v));
          b := CAS(x, v, cas_to);
        }
      } else {
        b := false;
      }
    } while (!b);
  }
}

//
// From Caper/BoundedReadWriteLock
//

region RWLock(id r, id s, memcell x)
  guards {
    duplicable WLOCK;
    unique WUNLOCK;
    unique RLOCK1;
    unique RUNLOCK1;
    unique RLOCK2;
    unique RUNLOCK2;
  }
  interpretation {
    Cell(s, x, ?v) && G@s &&
    (v ==  0 ? RUNLOCK1@r && RUNLOCK2@r && WUNLOCK@r :
     v ==  1 ? WUNLOCK@r && RUNLOCK2@r :
     v ==  2 ? WUNLOCK@r && RUNLOCK1@r :
     v ==  3 ? WUNLOCK@r :
     v == -1 ? RUNLOCK1@r && RUNLOCK2@r :
     false)
  }
  state { v }
  actions {
    RLOCK1   :  0 ~> Set(0, 1);
    RUNLOCK1 :  1 ~> Set(1, 0);
    RLOCK2   :  0 ~> Set(0, 2);
    RUNLOCK2 :  2 ~> Set(2, 0);
    RLOCK1   :  2 ~> Set(2, 3);
    RUNLOCK1 :  3 ~> Set(3, 2);
    RLOCK2   :  1 ~> Set(1, 3);
    RUNLOCK2 :  3 ~> Set(3, 1);
    WLOCK    :  0 ~> Set(0, -1);
    WUNLOCK  : -1 ~> Set(-1, 0);
  }

procedure readerAcquire1(id r, id s, memcell x)
  requires RWLock(r, s, x, ?m) && RLOCK1@r && m != 1 && m != 3;
  ensures  RWLock(r, s, x, ?n) && RLOCK1@r && RUNLOCK1@r && n > 0;
{
  use_region_interpretation RWLock(r, s, x);
    // Learn that m != 1 && m != 3 implies that m in {-1, 0, 2}

  use_atomic
    using RWLock(r, s, x) with RLOCK1@r;
  {
    assert Cell(s, x, ?v); // Bind region value before call
    spinCAS(s, x, Set(-1, 0, 2));
      // cas_if := \v -> v >= 0
      // cas_to := \v -> v + 1
    assert Cell(s, x, ?w); // Bind region value after call
    assume v >= 0; // From cas_if
    assume w == v + 1; // From cas_to
  }
}

procedure readerRelease1(id r, id s, memcell x)
  requires RWLock(r, s, x, ?n) && n > 0 && RUNLOCK1@r;
  ensures  RWLock(r, s, x);
{
  use_region_interpretation RWLock(r, s, x);
    // Learn that n > 0 && RUNLOCK1@r implies that n in {1, 3}

  use_atomic
    using RWLock(r, s, x) with RUNLOCK1@r;
  {
    assert Cell(s, x, ?v); // Bind region value before call
    spinCAS(s, x, Set(1, 3));
      // cas_if := \v -> true
      // cas_to := \v -> v - 1
    assert Cell(s, x, ?w); // Bind region value after call
    assume true; // From cas_if
    assume w == v - 1; // From cas_to
  }
}

procedure readerAcquire2(id r, id s, memcell x)
  requires RWLock(r, s, x, ?m) && RLOCK2@r && m != 2 && m != 3;
  ensures  RWLock(r, s, x, ?n) && RLOCK2@r && RUNLOCK2@r && n > 0;
{
  use_region_interpretation RWLock(r, s, x);
    // Learn that m != 1 && m != 3 implies that m in {-1, 0, 1}

  use_atomic
    using RWLock(r, s, x) with RLOCK2@r;
  {
    assert Cell(s, x, ?v); // Bind region value before call
    spinCAS(s, x, Set(-1, 0, 1));
      // cas_if := \v -> v >= 0
      // cas_to := \v -> v + 2
    assert Cell(s, x, ?w); // Bind region value after call
    assume v >= 0; // From cas_if
    assume w == v + 2; // From cas_to
  }
}

procedure readerRelease2(id r, id s, memcell x)
  requires RWLock(r, s, x, ?n) && n > 0 && RUNLOCK2@r;
  ensures  RWLock(r, s, x);
{
  use_region_interpretation RWLock(r, s, x);
    // Learn that n > 0 && RUNLOCK2@r implies that n in {2, 3}

  use_atomic
    using RWLock(r, s, x) with RUNLOCK2@r;
  {
    assert Cell(s, x, ?v); // Bind region value before call
    spinCAS(s, x, Set(2, 3));
      // cas_if := \v -> true
      // cas_to := \v -> v - 2
    assert Cell(s, x, ?w); // Bind region value after call
    assume true; // From cas_if
    assume w == v - 2; // From cas_to
  }
}

procedure writerAcquire(id r, id s, memcell x)
  requires RWLock(r, s, x) && WLOCK@r;
  ensures  RWLock(r, s, x, -1) && WLOCK@r && WUNLOCK@r;
{
  bool b;

  use_atomic
    using RWLock(r, s, x) with WLOCK@r;
  {
    assert Cell(s, x, ?v); // Bind region value before call
    spinCAS(s, x, Int);
      // cas_if := \v -> v == 0
      // cas_to := \v -> -1
    assert Cell(s, x, ?w); // Bind region value after call
    assume v == 0; // From cas_if
    assume w == -1; // From cas_to
  }
}

abstract_atomic procedure writerReleaseAA(id s, memcell x)
  interference ?m in Set(-1) on s;
  requires Cell(s, x, m) && G@s;
  ensures  Cell(s, x, 0) && G@s;
{
  use_atomic
    using Cell(s, x) with G@s;
  {
    x.f := 0;
  }
}

procedure writerRelease(id r, id s, memcell x)
  requires RWLock(r, s, x, -1) && WUNLOCK@r;
  ensures  RWLock(r, s, x);
{
  use_atomic
    using RWLock(r, s, x) with WUNLOCK@r;
  {
    writerReleaseAA(s, x);
  }
}

//
// From Caper/CAPLock
//

predicate Inv() /* Invariant of CAP lock (left abstract) */

region CAPLock(id r, id s, memcell x)
  guards {
    duplicable Z;
    unique K;
  }
  interpretation {
       Cell(s, x, ?v) && G@s
    && (v == 0 ==> K@r && Inv())
    && (v == 1 ==> true)
  }
  state { v }
  actions {
    Z: 0 ~> Set(0, 1);
    K: 1 ~> Set(1, 0);
  }

procedure lock(id r, id s, memcell x)
  requires CAPLock(r, s, x, ?m) && (m in Set(0, 1)) && Z@r;
  ensures  CAPLock(r, s, x, ?n) && n == 1 && K@r && Z@r;
{
  use_atomic
    using CAPLock(r, s, x) with Z@r;
  {
    assert Cell(s, x, ?v); // Bind region value before call
    spinCAS(s, x, Int);
      // cas_if := \v -> v == 0
      // cas_to := \v -> 1
    assert Cell(s, x, ?w); // Bind region value after call
    assume v == 0; // From cas_if
    assume w == 1; // From cas_to
  }
}

abstract_atomic procedure unlockAA(id s, memcell x)
  interference ?v in Set(1) on s;
  requires Cell(s, x, v) && G@s;
  ensures Cell(s, x, 0) && G@s;
{
  make_atomic
    using Cell(s, x) with G@s;
  {
    update_region
      using Cell(s, x);
    {
      x.f := 0;
    }
  }
}

procedure unlock(id r, id s, memcell x)
  requires CAPLock(r, s, x, ?m) && m == 1 && K@r;
  requires Inv();
{
  use_atomic
    using CAPLock(r, s, x) with K@r;
  {
    unlockAA(s, x);
  }
}



struct memcell {
  int f;
}

primitive_atomic procedure CAS(memcell x, int now, int thn) returns (bool ret)
  requires x.f |-> ?v;
  ensures  v == now
              ? ret && x.f |-> thn
              : !ret && x.f |-> v;
