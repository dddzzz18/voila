//:: IgnoreFile(/voila/issue/41/)

// TODO: Currently (nondeterministically?) crashes Silicon - find out, why

// Corresponds to:
//   https://github.com/caper-tool/caper/blob/master/examples/iterative/Queue.t
//

struct queue {
  node sentinel;
  id _nextrid; // ghost
  int _nextlvl; // ghost
}

struct node {
  int value;
  node next;
  int _absstate; // ghost
  id _nextrid; // ghost
  int _nextlvl; // ghost
}

predicate Inv(int v)

region Qu(id q_r, int q_lvl, queue q)
  guards { duplicable G; }
  interpretation {
////    0 : p |-> x &*& QL(rs,x,_,3*n+1) &*& n >= 0 &*& rs@OWN;
    q.sentinel |-> ?sl && sl != null &&
//    sentinel._absstate |-> ?x_a && x_a >= 0 && x_a mod 3 == 1 &&
    q._nextrid |-> ?sl_r &&
    q._nextlvl |-> ?sl_lvl && sl_lvl < q_lvl &&
    QL(sl_r, sl_lvl, sl, ?sl_a) && sl_a >= 0 && sl_a mod 3 == 1 &&
    OWN@sl_r
  }
  state { 0 }
  actions {}

region QL(id r, int lvl, node x/*, int v */) // TODO: Add v as an out-argument
  guards {
    duplicable Z;
    unique OWN;
  }
  interpretation {
////                0 : x |-> v &*& (x+1) |-> 0 &*& queueInvariant(v);
////                1 : x |-> v &*& (x+1) |-> 0;
////    n > 0 | 3 * n : x |-> v &*& (x+1) |-> n &*& queueInvariant(v) &*& QL(rn,n,_,3*b) &*& rn@OWN;
////    n > 0 | 3*n+1 : x |-> v &*& (x+1) |-> n &*& QL(rn,n,_,3*b) &*& rn@OWN;
////    n > 0 | 3*n+2 : x |-> v &*& (x+1) |-> n &*& r@OWN &*& QL(rn,n,_,_);
//
// In the Caper specs, the value 'v' of the current node is part of the region arguments,
// and the value of the next node is always existentially quantified (but irrelevant, i.e. '_');
// in method specs, it is always irrelevant. That is probably to account for the fact that
// the values in the queue are never fully known due to possible concurrent modifications.
// A similar argument applies to the IDs of subsequent nodes, which are also existentially
// quantified ('rn').
//
// This kind of existential quantification, however, is not supported in Voila. Our initial
// idea of storing the values and IDs of the queue in sequence-typed region arguments,
// e.g. 'seq<int> vs' and 'seq<id> rs', and using the values for subsequent nodes, e.g.
// 'QL(head(rs), y, head(vs), tail(rs), tail(vs))' unfortunately wouldn't account for the
// concurrent nature of the queue ... at least I am not yet convinced that it would.
// Well, it probably would be if these arguments were havocked as well, which currently isn't
// the case.
//
// Solution:
//   - Store region IDs in ghost fields
//   - Remove values from region arguments until Voila has proper out-arguments
//
    (x.value |-> ?v) &&
    (x.next |-> ?y) &&
    (x._absstate |-> ?a) &&
    (x._nextrid |-> ?y_r) &&
    (x._nextlvl |-> ?y_lvl) && y_lvl < lvl &&
    (  a == 0               ? (Inv(v) && y == null)
     : a == 1               ? (          y == null)
     : a >= 3 && a mod 3 == 0 ? (Inv(v) && OWN@y_r && QL(y_r, y_lvl, y, ?y_a) && y_a mod 3 == 0 && y != null)
     : a >= 4 && a mod 3 == 1 ? (          OWN@y_r && QL(y_r, y_lvl, y, ?y_b) && y_b mod 3 == 0 && y != null) // TODO: Binding a_y again here results in duplicated identifiers
     : a >= 5 && a mod 3 == 2 ? (          OWN@r   && QL(y_r, y_lvl, y)                       && y != null)
     : false)
  }
  state { a }
  actions {
/* TODO: Overlapping actions are not yet supported (issue #27).
 *       Currently, the actions are turned into a cascade of ite-expressions,
 *       starting with the last one.
 *       Note: The current order is crucial for verifying the example!
 */
////    n >= 3 | OWN : 0 ~> n;
    ?n | n >= 3                | OWN: 0 ~> n;
////    n > 0  | OWN : 1 ~> 3*n+2;
    ?n | n > 0 && n mod 3 == 2 | OWN: 1 ~> n;

////    n > 0  |     : 0 ~> 3*n;
    ?n | n > 0 && n mod 3 == 0 | Z:   0 ~> n;
////    n > 0  |     : 1 ~> 3*n+1;
    ?n | n > 0 && n mod 3 == 1 | Z:   1 ~> n;

////    n >= 0 | OWN : 3*n ~> 3*n+1;
    ?n | n >=0 && n mod 3 == 0 | OWN: n ~> n + 1;
////    n > 0  | OWN : 3*n+1 ~> 3*n+2;
    ?n | n > 0 && n mod 3 == 1 | OWN: n ~> n + 1;
////    n > 0  | OWN : 3*n ~> 3*n+2;
    ?n | n > 0 && n mod 3 == 0 | OWN: n ~> n + 2;
  }

procedure makeQueue() returns (id r, int lvl, queue ret)
  ensures Qu(r, lvl, ret, 0);
{
  queue x;
  node y;
  id rs; // ghost
  int rlvl; // ghost

  // y := alloc(2);
  inhale y.value |-> _;
  inhale y.next |-> _;

  // [y + 1] := 0;
  y.next := null;

  // x := alloc(1);
  inhale x.sentinel |-> _;

  // [x] := y;
  x.sentinel := y;

  // return x;
  ret := x;

  inhale y._nextrid |-> _; // ghost
  inhale y._nextlvl |-> _; // ghost
  inhale y._absstate |-> 1; // ghost
  inhale OWN@rs; // ghost
  fold QL(rs, rlvl, y); // ghost: Close newly-allocated region

  inhale x._nextrid|-> rs; // ghost
  inhale x._nextlvl|-> rlvl && rlvl < lvl; // ghost
  fold Qu(r, lvl, ret); // ghost: Close newly-allocated region
}

procedure getTail(id r, int lvl, node x) returns (node y, id y_r, int y_lvl) // y_r and y_lvl are ghost
  requires QL(r, lvl, x) && x != null;
  ensures  QL(y_r, y_lvl, y) && y != null;
{
  node n;
  bool cont;

  y := x;

  // ghost assignment to y_r
  havoc y_r;
  assume y_r == r;

  havoc y_lvl;
  assume y_lvl == lvl;

  cont := true;

  while (cont)
    invariant QL(y_r, y_lvl, y) && y != null;
  {
    open_region
      using QL(y_r, y_lvl, y);
    {
      n := y.next;

      // Ghost code
      assert y._nextrid |-> ?n_r; // Let n_r be the region id of the next node
      assert y._nextlvl |-> ?n_lvl; // Let n_lvl be the region lvl of the next node
      assert n != null ==> QL(n_r, n_lvl, n); // If next node exist, we should hold QL(n_r, n_lvl, n)
      inhale n != null ==> QL(n_r, n_lvl, n); // In this case, duplicate the region assertion
    }

    if (n == null) {
      // return y;
      cont := false;
    } else {
      y := n;

      // ghost assignment to y_r
      havoc y_r;
      assume y_r == n_r;

      // ghost assignment to y_lvl
      havoc y_lvl;
      assume y_lvl == n_lvl;
    }
  }
}

procedure enqueue(id s, int lvl, queue x, int v)
  requires Qu(s, lvl, x, 0) && Inv(v);
  ensures  Qu(s, lvl, x, 0);
{
  node y;
  node h;
  node t;
  id y_r; // ghost
  int y_lvl; // ghost
  id t_r; // ghost
  int t_lvl; // ghost
  bool b;
  bool cont;

//  y := alloc(2);
  inhale y.value |-> _;
  inhale y.next |-> _;

//  [y] := v;
//  [y + 1] := 0;
  y.value := v;
  y.next := null;

  cont := true;

  while (cont)
    invariant Qu(s, lvl, x, 0);
    invariant cont ==> y.value |-> v && y.next |-> null && Inv(v);
  {
//    h := [x];
    open_region
      using Qu(s, lvl, x);
    {
      h := x.sentinel;

      assert x._nextrid |-> ?h_r; // ghost: Bind first queue node's region id
      assert x._nextlvl |-> ?h_lvl; // ghost: Bind first queue node's region lvl
      assert QL(h_r, h_lvl, h); // ghost: Show that QL(h_r, h_lvl, h) is accessible ...
      inhale QL(h_r, h_lvl, h); // ghost: ... and duplicate the region assertion
    }

//    t := getTail(h);
    t, t_r, t_lvl := getTail(h_r, h_lvl, h);

//    cr := CAS(t + 1, 0, y);
    use_atomic
      using QL(t_r, t_lvl, t) with Z@t_r;
    {
      b := CAS_next(t, null, y); // Try to append new node to tail

      // If append succeeded, perform various ghost operations
      if (b) {
        inhale y._nextrid |-> _;
        inhale y._nextlvl |-> _;
        inhale y._absstate |-> 0; // Choose abstract state of newly-allocated node region
        havoc y_r;
        havoc y_lvl;
        inhale OWN@y_r; // Gain new node's guard
        fold QL(y_r, y_lvl, y); // Close newly-allocated region

        assert t._absstate |-> ?t_a0; // Bind previous tail's abstract state
        assert t_a0 == 0 || t_a0 == 1; // State was in one of two states [DEBUG]
        exhale t._absstate |-> t_a0; // Havoc abstract state
        inhale t._absstate |-> ?t_a1;
        assume t_a1 >= 3;
        assume t_a0 == 0 ==> t_a1 mod 3 == 0; // Choose new abstract state ...
        assume t_a0 == 1 ==> t_a1 mod 3 == 1; // ... depending on previous state
        exhale t._nextrid |-> _;   // Havoc next region's id ...
        inhale t._nextrid |-> y_r; // ... and set to y_r
        exhale t._nextlvl |-> _;   // Havoc next region's lvl ...
        inhale t._nextlvl |-> y_lvl; // ... and set to y_lvl
      }
    }

//    if (cr != 0) {
//      return;
//    }
    if (b) {
      cont := false;
    }
  }
}

procedure dequeue(id q_r, int q_lvl, queue q) returns (bool success, int v)
  requires Qu(q_r, q_lvl, q, 0);
  ensures  success ==> Inv(v);
{
  node sl;
  node hd;
  bool done;

  done := false;
  success := false;

  while (!done)
    invariant Qu(q_r, q_lvl, q, 0);
    invariant success ==> Inv(v);
//    invariant success ==> false;
  {
//    sentinel := [q];
    open_region
      using Qu(q_r, q_lvl, q);
    {
      sl := q.sentinel;
      // ghost: Duplicate sentinel's region assertion
      assert q._nextrid |-> ?sl_r0;
      assert q._nextlvl |-> ?sl_lvl0;
      assert QL(sl_r0, sl_lvl0, sl, ?sl_a0);
      inhale QL(sl_r0, sl_lvl0, sl);
      assert sl_a0 >= 0;     // [DEBUG]
      assert sl_a0 mod 3 == 1; // [DEBUG]
    }

//    head := [sentinel + 1];
    open_region
      using QL(sl_r0, sl_lvl0, sl);
    {
      hd := sl.next;
      // ghost: If head present, duplicate its region assertion
      if (hd != null) {
        assert sl._nextrid |-> ?hd_r0;
        assert sl._nextlvl |-> ?hd_lvl0;
        assert QL(hd_r0, hd_lvl0, hd, ?hd_a0);
        inhale QL(hd_r0, hd_lvl0, hd);
      }
    }

    if (hd == null) {
//      return 0;
      done := true;
      success := false;
    } else {
//      r := CAS(q, sentinel, hd);
      use_atomic
        using Qu(q_r, q_lvl, q) with G@q_r;
      {
        assert q._nextrid |-> ?sl_r1;
        assert q._nextlvl |-> ?sl_lvl1;
        assert q.sentinel |-> ?sl1;
        assert QL(sl_r1, sl_lvl1, sl1, ?sl_a1);

        (
          success := CAS_sentinel(q, sl, hd);
          // If dequeue succeeded, perform various ghost operations
          if (success) {
            // Since the CAS succeeded, q.sentinel was sl before the CAS.
            assert sl1 == sl;      // [DEBUG]
            assume sl_r1 == sl_r0; // TODO: Implied by sl1 == sl
            assume sl_lvl1 == sl_lvl0; // TODO: Implied by sl1 == sl
            assert sl_a1 >= 0;     // [DEBUG]
            assert sl_a1 mod 3 == 1; // [DEBUG]

            exhale q._nextrid |-> _;
            inhale q._nextrid |-> hd_r0;

            exhale q._nextlvl |-> _;
            inhale q._nextlvl |-> hd_lvl0;

            use_atomic // TODO: Reconsider which rule to use here
              using QL(sl_r0, sl_lvl0, sl) with OWN@sl_r0;
            {
              assert sl.next |-> ?hd1;
              if (hd1 != hd) {
                assume false;
                  // TODO: Provable in Caper (i.e. unreachable), but not in Voila.
                  //
                  // My current understanding is the following: without knowing that sentinel
                  // still precedes head we won't get OWN for head out of the sentinel (we would
                  // get it for some other node), which in turn would prevent us from changing
                  // the abstract state of the head region, which we need in order to take the
                  // invariant out of the region.
                  //
                  // I believe that Caper can prove that the sentinel's successor cannot have
                  // changed in the meantime because the action used there effectively disallow
                  // changing the next pointer once it is non-null.
              }

              assert sl._nextrid |-> ?hd_r1;
              assume hd_r1 == hd_r0; // TODO: Implied by hd1 == hd

              assert sl._nextlvl |-> ?hd_lvl1;
              assume hd_lvl1 == hd_lvl0; // TODO: Implied by hd1 == hd

              assert sl_a1 mod 3 == 1; // [DEBUG]
              assert sl_a1 >= 4;     // [DEBUG]
              exhale sl._absstate |-> sl_a1;
              inhale sl._absstate |-> sl_a1 + 1;
              assert (sl_a1 + 1) >= 5;     // [DEBUG]
              assert (sl_a1 + 1) mod 3 == 2; // [DEBUG]

              use_atomic // TODO: Reconsider which rule to use here
                using QL(hd_r0, hd_lvl0, hd) with OWN@hd_r0;
              {
                assert hd.value |-> ?v0;
                assert hd._absstate |-> ?u40;
                assert u40 mod 3 == 0;          // [DEBUG]
                assert u40 == 0 || u40 >= 3;  // [DEBUG]
                assert Inv(v0);               // [DEBUG]
                assume u40 == 0; // TODO: Get rid of (case split?) Could also be a non-issue once overlapping actions are supported.
                exhale hd._absstate |-> u40;
                inhale hd._absstate |-> u40 + 1;
              }

              // Duplicate region assertion
              assert QL(hd_r0, hd_lvl0, hd);
              inhale QL(hd_r0, hd_lvl0, hd);
            }
          }
        );

//        if (r != 0)
        if (success) {
//          res := [head];
//          return res;
          open_region
            using QL(hd_r0, hd_lvl0, hd);
          {
            v := hd.value;
            assume v == v0; // TODO: Holds in Caper because hd.value is immutable
          }
        }
      }
    }
  }
}



primitive_atomic procedure CAS_next(node x, node now, node thn) returns (bool ret)
  requires x.next |-> ?v;
  ensures  v == now
              ? ret && x.next |-> thn
              : !ret && x.next |-> v;

primitive_atomic procedure CAS_sentinel(queue x, node now, node thn) returns (bool ret)
  requires x.sentinel |-> ?v;
  ensures  v == now
              ? ret && x.sentinel |-> thn
              : !ret && x.sentinel |-> v;
