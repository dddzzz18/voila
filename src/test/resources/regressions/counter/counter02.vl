struct memcell {
  int f;
}

region Counter(id c, memcell x)
  guards { duplicable G; }
  interpretation {
    x.f |-> ?n
  }
  state { n }
  actions {
    G: ?m ~> Int;
  }



abstract_atomic procedure read01(id c, memcell x) returns (int ret)
  interference ?n in Int on c;
  requires Counter(c, x, n) && G@c;
  ensures Counter(c, x, n) && G@c;
  ensures ret == n;
{
  open_region
    using Counter(c, x);
  {
    ret := x.f;
  }
}

abstract_atomic procedure read02(id c, memcell x) returns (int ret)
  interference ?n in Set[int](n) on c;
  requires Counter(c, x, n) && G@c;
  ensures Counter(c, x, n) && G@c;
  ensures ret == n;
{
  open_region
    using Counter(c, x);
  {
    ret := x.f;
  }
}



procedure read01_client01(id c, memcell x) returns (int ret)
  requires Counter(c, x, ?n) && G@c;
  ensures Counter(c, x, ?m) && G@c;
  ensures ret == n;
{
  ret := read01(c, x);
}

procedure read01_client02(id c, memcell x) returns (int ret)
  requires Counter(c, x, ?n) && G@c;
  ensures Counter(c, x, ?m) && G@c;
  //:: ExpectedOutput(postcondition_error:assertion_error)
  ensures ret == n;
{
  skip;
  ret := read01(c, x);
}

procedure read01_client03(id c, memcell x) returns (int ret)
  requires Counter(c, x, ?n) && G@c;
  ensures ret == n;
  //:: ExpectedOutput(postcondition_error:region_state_error)
  ensures Counter(c, x, n) && G@c;
{
  int v;
  v := read01(c, x);
  ret := v;
}



procedure read02_client01(id c, memcell x) returns (int ret)
  requires Counter(c, x, ?n) && G@c; // TODO: Unstable, see Voila issue #15
{
  //:: ExpectedOutput(precondition_error:interference_error)
  ret := read02(c, x);
}

procedure read02_client02(id c, memcell x) returns (int ret)
  // interference ?n in Set[int](n) on c; // TODO: See Voila issue #14
  requires Counter(c, x, ?n) && G@c;      // TODO: Unstable, see Voila issue #15
  ensures ret == n;
  ensures Counter(c, x, n) && G@c;
{
  //:: ExpectedOutput(precondition_error:interference_error)
  ret := read02(c, x);
}

procedure read02_client03(id c, memcell x) returns (int ret)
  requires Counter(c, x, ?n) && G@c; // TODO: Unstable, see Voila issue #15
{
  skip;
  //:: ExpectedOutput(precondition_error:interference_error)
  ret := read02(c, x);
}


region SeqCounter(id k, id c, memcell x)
  guards {
    unique S;
  }
  interpretation {
    Counter(c, x, ?n) && G@c
  }
  state { n }
  actions {
    S: ?m ~> Int;
  }

region ParCounter(id k, id c, memcell x)
  guards {
    duplicable P;
  }
  interpretation {
    Counter(c, x, ?n) && G@c
  }
  state { n }
  actions {
    P: ?m ~> Int;
  }

procedure read01_seqclient01(id k, id c, memcell x) returns (int ret)
  requires SeqCounter(k, c, x) && S@k;
  ensures  SeqCounter(k, c, x) && S@k;
{
  skip;
  skip;

  use_atomic
    using SeqCounter(k, c, x) with S@k;
  {
    ret := read01(c, x); // CALL OK
  }
}

procedure read01_parclient01(id k, id c, memcell x) returns (int ret)
  requires ParCounter(k, c, x) && P@k;
  ensures  ParCounter(k, c, x) && P@k;
{
  skip;
  skip;

  use_atomic
    using ParCounter(k, c, x) with P@k;
  {
    ret := read01(c, x); // CALL OK
  }
}

procedure read02_seqclient01(id k, id c, memcell x) returns (int ret)
  requires SeqCounter(k, c, x) && S@k;
  ensures  SeqCounter(k, c, x) && S@k;
{
  skip;
  skip;

  use_atomic
    using SeqCounter(k, c, x) with S@k;
  {
    ret := read02(c, x); // CALL OK
  }
}

procedure read02_parclient01(id k, id c, memcell x) returns (int ret)
  requires ParCounter(k, c, x) && P@k;
  ensures  ParCounter(k, c, x) && P@k;
{
  skip;
  skip;

  use_atomic
    using ParCounter(k, c, x) with P@k;
  {
    //:: ExpectedOutput(precondition_error:interference_error)
    ret := read02(c, x);
  }
}
