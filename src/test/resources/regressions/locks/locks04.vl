struct memcell {
  int f;
}

//:: UnexpectedOutput(region_actions_not_transitive_error, /voila/issue/95/)
region Lock(id a, int lvl, memcell x)
  guards { duplicable G; }
  interpretation {
    x.f |-> ?v && (v == 0 || v == 1)
  }
  state { v }
  actions {
    G: 0 ~> 1;
    G: 1 ~> 0;
  }

predicate Inv()

//:: ExpectedOutput(region_interpretation_not_stable_error)
//:: UnexpectedOutput(region_actions_not_transitive_error, /voila/issue/95/)
region CAPLock(id r, int alvl, id a, int lvl, memcell x)
  guards {
    duplicable Z;
    unique K;
  }
  interpretation {
       Lock(a, lvl, x, ?v) && lvl < alvl && G@a
    && (v == 0 ==> K@r && Inv())
    && (v == 1 ==> true)
  }
  state { v }
  actions {
    Z: 0 ~> 1;
    Z: 1 ~> 0; // [XXX] Added action: Z also allows to unlock
    K: 1 ~> 0;
  }



abstract_atomic procedure lock(id a, int lvl, memcell x)
  interference ?s in Set(0, 1) on a;
  requires Lock(a, lvl, x, s) && G@a;
  ensures Lock(a, lvl, x, 1) && G@a && s == 0;

abstract_atomic procedure unlock(id a, int lvl, memcell x)
  interference ?s in Set(1) on a;
  requires Lock(a, lvl, x, s) && G@a;
  ensures Lock(a, lvl, x, 0) && G@a;



procedure test01(id r, int alvl, id a, int lvl, memcell x)
  requires CAPLock(r, alvl, a, lvl, x, ?v) && (v == 0 || v == 1) && Z@r;
  //:: ExpectedOutput(postcondition_error:assertion_error)
  ensures CAPLock(r, alvl, a, lvl, x, ?w) && w == 1 && K@r;
{
  use_atomic
    using CAPLock(r, alvl, a, lvl, x) with Z@r;
  {
    lock(a, lvl, x);
  }
}
